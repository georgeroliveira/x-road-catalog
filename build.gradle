buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.owasp:dependency-check-gradle:${owaspVersion}"
    }
}

plugins {
    id "com.github.hierynomus.license" version "${hierynomusLicenseVersion}"
    id "org.sonarqube" version "${sonarQubeVersion}"
    id "jacoco"
}

apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'idea'
apply plugin: 'java'

idea {
    project {
        //if you want to set specific jdk and language level
        jdkName = '11'
        languageLevel = '11'
    }
    module {
        downloadSources = true
    }
}

idea.module {
    excludeDirs -= file(buildDir)
    buildDir.listFiles({ d, f -> f != 'generated-sources/wsimport' } as FilenameFilter).each { excludeDirs += it }
}

wrapper {
    gradleVersion = '7.6'
}

ext {
    project_version='3.0.0'
}


allprojects {
    group = 'fi.vrk.xroad'
    version = project_version
}

subprojects {
    apply plugin: 'com.github.hierynomus.license'

    license {
        header rootProject.file('LICENSE.txt')
        include "**/*.java"
        exclude "**/wsimport/*"
    }
}


