plugins {
    id "com.github.hierynomus.license" version "${hierynomusLicenseVersion}"
    id "org.owasp.dependencycheck" version "${owaspVersion}"
    id "org.sonarqube" version "${sonarQubeVersion}"
    id 'idea'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

idea {
    project {
        jdkName = '11'
        languageLevel = '11'
    }
    module {
        downloadSources = true
        excludeDirs -= file(buildDir)
        buildDir.listFiles({ d, f -> f != 'generated-sources/wsimport' } as FilenameFilter).each { excludeDirs += it }
    }
}

wrapper {
    gradleVersion = '7.6'
}

ext {
    project_version='3.0.5'
}


allprojects {
    group = 'fi.vrk.xroad'
    version = project_version
}

subprojects {
    apply plugin: 'com.github.hierynomus.license'

    license {
        header rootProject.file('LICENSE.txt')
        include "**/*.java"
        exclude "**/wsimport/*"
    }
}


