plugins {
    id "org.springframework.boot" version "${springBootVersion}"
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'jacoco'
}

group 'fi.vrk.xroad.catalog.persistence'
sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}")
    implementation("com.h2database:h2:${h2DatabaseVersion}")
    implementation("org.postgresql:postgresql:${postgreSqlVersion}")
    implementation("com.google.guava:guava:${guavaVersion}")
    implementation("org.projectlombok:lombok:${lombokVersion}")
    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    testImplementation("org.projectlombok:lombok:${lombokVersion}")
    testAnnotationProcessor("org.projectlombok:lombok:${lombokVersion}")
}


test {
    systemProperty 'user.timezone', 'Europe/Helsinki'
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
    }
    finalizedBy jacocoTestReport
}

tasks.withType(org.springframework.boot.gradle.tasks.run.BootRun) {
    systemProperty 'spring.profiles.active', System.getProperty('spring.profiles.active', 'default')
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = ''
}

springBoot {
    mainClass = 'fi.vrk.xroad.catalog.persistence.Application'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}


