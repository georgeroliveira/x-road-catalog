plugins {
    alias (libs.plugins.spring.boot)
    alias (libs.plugins.wsdl2java)
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'jacoco'
    id 'catalogservice.java-checks'
}

group 'fi.vrk.xroad.catalog.lister'
sourceCompatibility = 21
targetCompatibility = 21

repositories {
    mavenCentral()
}

wsdl2java {
    cxfVersion.set(libs.versions.cxf) // Grab the version from our versions catalog
    markGenerated = true
    packageName = 'fi.vrk.xroad.catalog.lister.generated'
}

bootJar {
    enabled = true
    layered {
        enabled = false
    }
}

jar {
    enabled = false
}

dependencies {
    implementation project(':xroad-catalog-persistence')
    implementation (libs.jakarta)
    implementation (libs.spring.doc)
    implementation (libs.commons.csv)
    implementation (libs.spring.boot.ws)
    implementation (libs.spring.boot.jpa)
    implementation (libs.jackson)
    implementation (libs.wsdl4j)
    implementation (libs.commons.bean.utils)
    implementation (libs.guava)
    implementation (libs.lombok)
    annotationProcessor (libs.lombok)
    testImplementation (libs.spring.boot.test)
    testImplementation (libs.lombok)
    testAnnotationProcessor (libs.lombok)
}

test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
    }
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
    systemProperty 'spring.config.name', 'lister,catalogdb'
    finalizedBy jacocoTestReport
}

bootRun {
    systemProperty 'spring.config.name', 'lister,catalogdb'
}

springBoot {
    mainClass = 'fi.vrk.xroad.catalog.lister.ListerApplication'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}
