plugins {
    alias (libs.plugins.spring.boot)
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'jacoco'
}

group 'fi.vrk.xroad.catalog.lister'
sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schema = "src/main/resources/services.wsdl"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir, schema: schema) {
                arg(value: "-wsdl")
                produces(dir: sourcesDir, includes: "**/*.java")
            }

            javac(destdir: classesDir, includeantruntime:false, source: 11, target: 11, debug: true,
                    debugLevel: "lines,vars,source",
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: "**/*.java")
                include(name: "*.java")
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: "**/*.java")
                }
            }
        }
    }
}

task afterEclipseImport {
	dependsOn "genJaxb"
}

configurations {
    jaxb
}

bootJar {
    enabled = true
    layered {
        enabled = false
    }
}

jar {
    enabled = false
}

dependencies {
    implementation project(':xroad-catalog-persistence')
    implementation(files(genJaxb.classesDir).builtBy(genJaxb))

    implementation (libs.jakarta)
    implementation (libs.spring.doc)
    implementation (libs.commons.csv)
    implementation (libs.spring.boot.ws)
    implementation (libs.spring.boot.jpa)
    implementation (libs.jackson)
    implementation (libs.wsdl4j)
    implementation (libs.commons.bean.utils)
    implementation (libs.guava)
    jaxb (libs.xjc)
    implementation (libs.lombok)
    annotationProcessor (libs.lombok)
    testImplementation (libs.spring.boot.test)
    testImplementation (libs.lombok)
    testAnnotationProcessor (libs.lombok)
}

test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
    }
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
    systemProperty 'spring.config.name', 'lister,catalogdb'
    finalizedBy jacocoTestReport
}

bootRun {
    systemProperty 'spring.config.name', 'lister,catalogdb'
}

springBoot {
    mainClass = 'fi.vrk.xroad.catalog.lister.ListerApplication'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}
